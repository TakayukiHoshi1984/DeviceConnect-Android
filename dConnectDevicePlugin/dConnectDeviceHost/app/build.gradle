apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.0'

    def getVersionName = { ->
        def version
        def date = new java.text.SimpleDateFormat("yyyyMMdd").format(new Date())
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git rev-parse --short HEAD"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git rev-parse --short HEAD"].execute().in.text.trim()
        }
        return "v2.0.0-${date}-${version}"
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName getVersionName()
    }

    signingConfigs {
        releaseConfig {
            storeFile=file(project.properties.storeFile)
            storePassword=project.properties.storePassword
            keyAlias=project.properties.keyAlias
            keyPassword=project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseConfig
        }
    }

    productFlavors {
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile project(':dconnect-device-plugin-sdk')
}
